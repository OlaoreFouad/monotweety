apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
//apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

apply from: rootProject.file("gradle/jacoco.gradle")

def getSecretProperty(name) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('secret.properties').newDataInputStream())

    return properties.getProperty(name.toString())
}

def FABRIC_API_KEY = getSecretProperty('fabric.api.key')
def TWITTER_API_KEY = "\"${getSecretProperty('twitter.api.key')}\""
def TWITTER_API_SECRET = "\"${getSecretProperty('twitter.api.secret')}\""

android {
    compileSdkVersion 29

    signingConfigs {
        debug {
            storeFile file("../debug.keystore")
            storePassword 'android'
            keyPassword 'android'
        }
        release {
            storeFile file("../${getSecretProperty("keystoreFile")}")
            storePassword getSecretProperty("keystorePassword")
            keyAlias getSecretProperty("keyAlias")
            keyPassword getSecretProperty("keyPassword")
        }
    }

    defaultConfig {
        applicationId "net.yslibrary.monotweety"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 58
        versionName "1.9.0"

        buildConfigField 'String', 'TWITTER_API_KEY', TWITTER_API_KEY
        buildConfigField 'String', 'TWITTER_API_SECRET', TWITTER_API_SECRET

        manifestPlaceholders = [fabricApiKey: FABRIC_API_KEY]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
            multiDexEnabled true
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    dexOptions {
        jumboMode true
    }

    lintOptions {
        lintConfig rootProject.file('gradle/lint.xml')
        abortOnError false
        textReport true
        textOutput 'stdout'
//    xmlReport true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    // rename release apk
    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.each { output ->
                if (output?.outputFileName?.endsWith(".apk")) {
                    final version = "v${defaultConfig.versionName}"
                    final name = "${["monotweety", variant.baseName, version].join("-")}.apk"
                    output.outputFileName = name
                }
            }
        }
    }
}

kapt {
    correctErrorTypes = true
    useBuildCache = true
    javacOptions {
        option("-Adagger.fastInit=enabled")
    }
}

ext.retrofit_version = '2.9.0'
ext.androidx_version = '1.0.0'
ext.rxbinding_version = '2.2.0'
ext.conductor_version = '3.0.0-rc6'
ext.okhttp_version = '4.9.0'
ext.dagger_version = '2.29.1'
ext.storio_version = '3.0.0'
ext.leakcanary_version = '2.4'
ext.glide_version = '4.11.0'
dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.multidex:multidex:2.0.1"

    implementation platform('com.google.firebase:firebase-bom:25.10.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    implementation 'com.twitter.twittertext:twitter-text:3.1.0'
    implementation 'com.twitter.sdk.android:twitter-core:3.3.0'

    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"

    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    implementation "com.pushtorefresh.storio3:sqlite:$storio_version"

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    implementation 'com.gojuno.koptional:koptional:1.7.0'
    implementation 'com.gojuno.koptional:koptional-rxjava2-extensions:1.7.0'

    implementation 'com.artemzin.rxjava:proguard-rules:1.3.3.0'

    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxbinding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$rxbinding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rxbinding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:$rxbinding_version"

    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'

    implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.1'

    implementation "com.bluelinelabs:conductor:${conductor_version}"
    implementation "com.bluelinelabs:conductor-androidx-transition:${conductor_version}"
    implementation "com.bluelinelabs:conductor-rxlifecycle2:${conductor_version}"

    implementation 'com.hannesdorfmann:adapterdelegates3:3.1.0'

    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.4'

    implementation 'net.yslibrary.licenseadapter:licenseadapter:3.1.0'

    implementation 'com.github.gabrielemariotti.changeloglib:changelog:2.1.0'

    implementation 'me.drakeet.support:toastcompat:1.1.0'

    implementation "com.squareup.leakcanary:plumber-android:$leakcanary_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    implementation "com.squareup.leakcanary:leakcanary-object-watcher-android:$leakcanary_version"

    debugImplementation 'com.facebook.stetho:stetho:1.5.1'

    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'junit:junit:4.13'
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation 'io.mockk:mockk:1.10.0'
    testImplementation "com.google.truth:truth:1.0.1"
}
